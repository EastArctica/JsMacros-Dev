plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

// Configuration for embedding extension jars
configurations {
    extensionJars {
        canBeResolved = true
        canBeConsumed = false
    }
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()

            // DataGen can be run by - "./gradlew :neoforge:runData" in Terminal.
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    // Common library dependencies (included via shadow or implementation)
    implementation group: 'io.noties', name: 'prism4j', version: '2.0.0'
    implementation group: 'org.jooq', name: 'joor', version: '0.9.15'
    implementation group: 'com.neovisionaries', name: 'nv-websocket-client', version: '2.14'
    implementation group: 'org.javassist', name: 'javassist', version: '3.30.2-GA'
    
    // Extension jars to embed
    extensionJars project(path: ':extension:graal', configuration: 'archives')
    extensionJars project(path: ':extension:graal:js', configuration: 'archives')
}

// Collect extension jar names for dependencies property
def getExtensionJarPaths() {
    return configurations.extensionJars.files.collect { file ->
        "\"META-INF/jsmacrosdeps/${file.name}\""
    }.join(', ')
}

processResources {
    // Add dependencies expansion for jsmacros.extension.json
    filesMatching('jsmacros.extension.json') {
        expand 'dependencies': getExtensionJarPaths()
    }
}

// Embed extension jars into the final jar
jar {
    dependsOn configurations.extensionJars
    from(configurations.extensionJars) {
        into 'META-INF/jsmacrosdeps'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
